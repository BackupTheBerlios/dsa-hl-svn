AC_PREREQ(2.50)

AC_INIT(RL, 0.11.0)
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE([1.6.1 foreign no-installinfo no-installman subdir-objects])
AC_CONFIG_HEADER(engine/common/include/config.h)
AC_CONFIG_MACRO_DIR([m4])

##AC_CANONICAL_TARGET

dnl Check for programs
AC_LANG([C++])
AC_PROG_CC
AC_PROG_CXX
AM_PROG_CC_C_O
AC_PROG_INSTALL
AM_PROG_LIBTOOL


dnl GCC version check
if test x$GCC = xyes; then
    AC_MSG_CHECKING([version 3.x of gcc])
    if test -n "`$CC --version 2>&1|grep '3.'`"; then
        AC_DEFINE(EXT_HASH, 1, Use old STL hash options)
        AC_DEFINE(GCC_3_1, 1, 3.x GCC version)
        AC_MSG_RESULT(yes);
    else
        AC_MSG_RESULT(no);
    fi
fi

# ***** DEBUGGING
AC_ARG_ENABLE(debug,[  --enable-debug          enable debugging [default=no]])
if test "$enable_debug" = "yes"; then
    CXXFLAGS="$CXXFLAGS -g -O0"
else
    CXXFLAGS="$CXXFLAGS -O2"
fi

# ***** PROFILING
AC_ARG_ENABLE(profiling,[  --enable-profiling  enable profiling [default=no]])
if test "$enable_profiling" = "yes"; then
    CXXFLAGS="$CXXFLAGS -pg -g"
    LDFLAGS="$LDFLAGS -pg -g"
fi

# Pthreads
CXXFLAGS="$CXXFLAGS -pthread"

#AC_ARG_ENABLE(test-coverage,[  --enable-test-coverage  enable coverage testing [default=no]])
#if test "$enable_test_coverage" = "yes"; then
#    CXXFLAGS="$CXXFLAGS -fprofile-arcs -ftest-coverage"
#fi

AC_ARG_ENABLE(tests,[  --enable-tests  enable unit testing [default=no]])
if test "$enable_tests" = "yes"; then
	AM_PATH_CPPUNIT(1.0.0, [], 
		[
			AM_CONDITIONAL(with_tests, [false])
		  	AC_MSG_ERROR([Cannot find cppunit libs needed for testing])
		])
	AM_CONDITIONAL(with_tests, [true])
else
    AM_CONDITIONAL(with_tests, [false])
fi


plugins=""
# fmod4
AC_ARG_ENABLE(fmod4, AC_HELP_STRING([--enable-fmod4], [enable fmod 4.xx (FMODex) support [default=off]]))
if test "$enable_fmod4" = "yes"; then
	AC_CHECK_LIB(fmodex, main, , 
		[
		  AM_CONDITIONAL(with_fmod4, [false])
		  AC_MSG_ERROR([Cannot find fmod 4.xx libs])
		])
	AC_CHECK_HEADERS(fmodex/fmod.hpp, , 
	    [
		  AM_CONDITIONAL(with_fmod4, [false])
	      AC_MSG_ERROR([Cannot find fmod headers])
	    ])
	AM_CONDITIONAL(with_fmod4, [true])
	plugins="$plugins fmod4driver"
else
    AM_CONDITIONAL(with_fmod4, [false])
fi

# openal
AC_ARG_ENABLE(openal, AC_HELP_STRING([--enable-openal], [enable OpenAL support [default=off]]))
if test "$enable_openal" = "yes"; then
	AC_CHECK_LIB(openal, main, , AC_MSG_ERROR([Cannot find OpenAL libs]))
	AC_CHECK_HEADERS(AL/al.h, , AC_MSG_ERROR([Cannot find OpenAL headers]))
	AM_CONDITIONAL(with_oal, [true])
	plugins="$plugins openaldriver"
else
        AM_CONDITIONAL(with_oal, [false])
fi

# Ogre
PKG_CHECK_MODULES(OGRE, OGRE >= 1.2.0)
AC_ARG_WITH(ogre-plugins,[  --with-ogre-plugins=path        Set location of Ogre plugins],[ OGRE_PLUGINDIR="$withval"], [OGRE_PLUGINDIR=/usr/local/lib/OGRE])
AC_SUBST(OGRE_PLUGINDIR)

# CEGUI
PKG_CHECK_MODULES(CEGUI, CEGUI >= 0.4.0)
PKG_CHECK_MODULES(CEGUIOGRE, CEGUI-OGRE)

# OpenSteer
AC_CHECK_HEADERS(OpenSteer/SteerLibrary.h, , AC_MSG_ERROR([Can't find OpenSteer headers]))
AC_CHECK_LIB(OpenSteer, main, , AC_MSG_ERROR([Can't find libOpenSteer]))

dnl Other stuff we need
AC_CHECK_LIB(m, pow)
AC_CHECK_LIB(z, gzopen)
AC_CHECK_FUNC(snprintf, AC_DEFINE(HAVE_SNPRINTF,,snprintf))
AC_CHECK_FUNC(vsnprintf, AC_DEFINE(HAVE_VSNPRINTF,1,vsnprintf))
AC_CHECK_PROG(HAVE_DOXYGEN_PROG, doxygen, `which doxygen`, false)
AC_CHECK_LIB([MadaBot], [main], [], AC_MSG_ERROR([Can't find libMadaBot]))

# Physics
AC_CHECK_LIB([Newton], [fopen], [], AC_MSG_ERROR([Can't find libNewton]) ) 
AC_CHECK_HEADERS(Newton.h, , AC_MSG_ERROR([Can't find Newton headers]))
OLDCXXFLAGS="$CXXFLAGS"
CXXFLAGS="$CXXFLAGS $OGRE_CFLAGS"
AC_CHECK_LIB([OgreNewt], [NewtonBallGetJointAngle], [], AC_MSG_ERROR([Can't find OgreNewt]) )
AC_CHECK_HEADERS(OgreNewt.h, , AC_MSG_ERROR([Can't find OgreNewt headers]))
CXXFLAGS="$OLDCXXFLAGS"

# XERCES
AC_CHECK_HEADERS(xercesc/util/XercesVersion.hpp, ,
    [AC_MSG_ERROR([cannot find libxerces headers]) ])
AC_CHECK_LIB(xerces-c,main,,
    [AC_MSG_ERROR([cannot find the libxerces library]) ])

# OIS
PKG_CHECK_MODULES(OIS, OIS >= 1.0.0)

# Boost
AX_BOOST_BASE(1.33.1)
AX_BOOST_THREAD()

dnl --------------------------------------------
dnl       determine location of RUBY (auto)
dnl --------------------------------------------

dnl determine location of ruby
AC_PATH_PROG(ruby_path, ruby, no)
if test $ruby_path = no; then
	AC_MSG_ERROR(Cannot find Ruby! This program requires Ruby-1.8.0)
fi

dnl determine if the installed Ruby is suitable for us
if `$ruby_path -e 'exit 1 if VERSION >= "1.8.0"'` ; then
	AC_MSG_ERROR(Your Ruby is too old! At least Ruby-1.8.0 is required)
fi

dnl locate "ruby.h", "intern.h"
ruby_h_dir=`$ruby_path -r mkmf -e 'print Config::CONFIG[["archdir"]]'`
RUBY_CFLAGS="-I$ruby_h_dir"

dnl locate libruby-static.a + extra libraries (libcrypt)
ruby_lib=`$ruby_path -r mkmf -e 'c=Config::CONFIG; libs=c[["libdir"]]+"/"+c[["LIBRUBY"]]+" "+c[["LIBS"]]; print libs'`
RUBY_LIBS=$ruby_lib

PLUGIN_DIRS=$plugins
AC_SUBST(RUBY_CFLAGS)
AC_SUBST(RUBY_LIBS)
AC_SUBST(PLUGIN_DIRS)

dnl Test for modified swig version
SWIG_PROG(1.3.29-pantheon)
SWIG_ENABLE_CXX
SWIG_RUBY

dnl Define the directory, where RL should look for modules
if test "x${prefix}" = "xNONE"; then
	prefix=${ac_default_prefix}
fi
dnl Must double eval here, to replace ${datadir} and after that ${prefix}
eval RL_MODULE_DIR="${datadir}"
eval RL_MODULE_DIR="${RL_MODULE_DIR}/rastullah"
AC_SUBST(RL_MODULE_DIR)

dnl Create the makefiles
AC_CONFIG_FILES([Makefile \
		docs/Makefile docs/src/Makefile \
		engine/Makefile \
		engine/ai/Makefile engine/ai/src/Makefile engine/ai/include/Makefile\
		engine/common/Makefile engine/common/src/Makefile engine/common/include/Makefile \
		engine/core/Makefile engine/core/src/Makefile engine/core/include/Makefile \
		engine/dialog/Makefile engine/dialog/src/Makefile engine/dialog/include/Makefile \
		engine/uicomponents/Makefile engine/uicomponents/src/Makefile engine/uicomponents/include/Makefile \
		engine/ui/Makefile engine/ui/src/Makefile engine/ui/include/Makefile \
		engine/rules/Makefile engine/rules/src/Makefile engine/rules/include/Makefile \
		engine/script/Makefile engine/script/swig/Makefile engine/script/src/Makefile \
		engine/startup/Makefile engine/startup/src/Makefile \
		tests/Makefile tests/coretests/Makefile tests/rulestests/Makefile tests/multimediatests/Makefile \
		tests/RlDialogTests/Makefile \
		plugins/Makefile \
		plugins/fmod4driver/Makefile plugins/fmod4driver/include/Makefile plugins/fmod4driver/src/Makefile \
		plugins/openaldriver/Makefile plugins/openaldriver/include/Makefile plugins/openaldriver/src/Makefile \
		rastullah.conf  \
		tools/Makefile \
    ])

AC_OUTPUT

eval GAME_DIR="${bindir}"
eval GAME_DIR="${GAME_DIR}"

dnl Show a summary about important directories
echo
echo "***********************************************************"
echo "* Rastullahs Lockenpracht - Configuration Results Summary *"
echo "***********************************************************"
echo
echo " Code options:"
echo "       Building RL in debug mode:          ${enable_debug-no}"
echo "       Enable unit testing:                ${enable_tests-no}"
echo
echo " Sound plugins:"
echo "       FMOD 4 plugin:                      ${enable_fmod4-no}"
echo "       OpenAL plugin:                      ${enable_openal-no}"
echo
echo " Directory settings:"
echo "       rastullah binary will be placed in: ${GAME_DIR}"
echo "       Module directory:                   ${RL_MODULE_DIR}"
echo
echo "Type make && make install and pray!"
